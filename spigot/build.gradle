
plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        url "https://libraries.minecraft.net/"
    }
    maven {
        url "https://jitpack.io/"
    }
}

dependencies {

    compileOnly("org.spigotmc:spigot:${project.ext.minecraftVersion}-R0.1-SNAPSHOT")

    compileOnly("com.github.MilkBowl:VaultAPI:1.7")

    shade(project(":api"))
    shade(project(":common"))

}

processResources {

    Map<String, ?> ex = new HashMap<>()
    ex.put("version", project.ext.fullVersion)
    ex.put("modid", project.ext.modid)
    ex.put("modname", project.ext.modname)
    ex.put("modclass", project.ext.modclass)

    from(sourceSets.main.resources.srcDirs) {
        include "plugin.yml"
        expand(ex)
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "plugin.yml"
    }
}

shadowJar {

    archiveBaseName.set("${project.ext.archiveName}-spigot")
    configurations = [project.configurations.shade]

    dependencies {
        exclude('net.minecraft:.*')
        exclude('net.spigotmc:.*')
        exclude('org.bukkit:.*')
        include(project(':api'))
        include(project(':common'))
    }
}

tasks.assemble.dependsOn tasks.shadowJar

artifacts {
    archives jar
    shadow sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifacts {
                jar
                sourcesJar
            }
        }
    }
    repositories {
        mavenLocal()
        maven {

            if (project.hasProperty("local") && project.getProperty("local") == "true") {
                url "file:" + projectDir.path + "/build/maven"
            } else if (project.hasProperty("mdUrl")) {
                url = project.mdUrl as String
                credentials {
                    username project.mdUser as String
                    password project.mdPass as String
                }
            }
        }
    }
}